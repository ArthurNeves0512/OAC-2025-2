.data
two_pi: .float 6.283185

.text
main:
    li a7, 6
    ecall                   

    fcvt.s.w fa7, zero      

    la t0, two_pi
    flw ft3, 0(t0)        

    # Lógica de redução: x_reduzido = x - (x / 2π) * 2π
    fdiv.s ft0, fa0, ft3    # x / 2π
    fcvt.w.s t0, ft0   # Pegando a parte inteira dos 360°
    fcvt.s.w ft1, t0        
    fmul.s ft2, ft1, ft3    # ft2 = número de voltas * 2π
    fsub.s fa0, fa0, ft2    # Agora fa0 está em [0, 2π) ou (-2π, 0)
    
    # Corrige se o resultado for negativo
    flt.s t1, fa0, fa7
    beqz t1, start_sincos
    fadd.s fa0, fa0, ft3    # Se x < 0, ajusta para o intervalo positivo [0, 2π)

start_sincos:
    li s0, 1               
    li t0, 10              

    fmv.s fs0, fa0          # fs0 = x (o ângulo reduzido)
    fmul.s fs1, fs0, fs0
    fneg.s fs1, fs1         # fs1 = -x^2 (fator comum para gerar os próximos termos)

    # Primeiros termos da série
    li a0, 1
    fcvt.s.w fa0, a0 # fa0 = 1.0
    fmv.s fa1, fs0 # fa1 = x

    # Termos atuais da série
    fmv.s fs2, fa0 #cosseno
    fmv.s ft0, fa1 #seno

sincos_loop:
    bge s0, t0, sincos_end_loop

    # termo_cos = termo_anterior * (-x^2) / ((2n)*(2n-1))
    slli t1, s0, 1          # t1 = 2n
    addi t2, t1, -1         # t2 = 2n-1
    fcvt.s.w ft1, t1
    fcvt.s.w ft2, t2
    fmul.s ft1, ft1, ft2    # ft1 = (2n)*(2n-1) (parte do fatorial)
    fmul.s fs2, fs2, fs1    # Numerador: multiplica pelo fator -x^2
    fdiv.s fs2, fs2, ft1    
    fadd.s fa0, fa0, fs2    

    # termo_sin = termo_anterior * (-x^2) / ((2n+1)*(2n))
    slli t1, s0, 1          # t1 = 2n
    addi t2, t1, 1          # t2 = 2n+1
    fcvt.s.w ft1, t1
    fcvt.s.w ft2, t2
    fmul.s ft1, ft1, ft2    # ft1 = (2n+1)*(2n) (parte do fatorial)
    fmul.s ft0, ft0, fs1    # Numerador: multiplica pelo fator -x^2
    fdiv.s ft0, ft0, ft1    
    fadd.s fa1, fa1, ft0    

    addi s0, s0, 1          
    j sincos_loop

sincos_end_loop:
    li a7, 2
    ecall                   
    
    fmv.s fa0, fa1
    ecall                   

    li a7, 10
    ecall     
